# Maintainer: Anatol Pomozov
# Contributor: Tommaso Sardelli <lacapannadelloziotom at gmail dot com>

pkgname=bpftrace
pkgver=0.11.4
pkgrel=2
pkgdesc='High-level tracing language for Linux eBPF'
arch=('x86_64' 'armv7h')
url='https://github.com/iovisor/bpftrace'
license=('Apache')
depends=('libelf' 'zlib' 'llvm-libs' 'clang' 'bcc' 'libbpf')
makedepends=('cmake' 'llvm' 'git' 'linux-headers')
options=(!strip)
source=("https://github.com/iovisor/bpftrace/archive/v$pkgver/bpftrace-$pkgver.tar.gz" 
        "add-arch-armv7h.patch")
sha512sums=('611a7e61dbd1f4cc52b7e51a1a143296ff7b2df115b3a28034c674d8eefb5d482cac551ab82d6b7cc2f6fc0668b07d2d9e283dff371fd9a3f649c80113fdca82'
            '9b66401928827cc0b58e7a74e3df53497569bd9ac364452eb4c79797939c6e053198e4a279811094cd1002af4db2d4717eae3930d3399aa86b1fc810861401af')

prepare() {
  cd bpftrace-$pkgver
  patch -p1 -i "$srcdir/add-arch-armv7h.patch"
}

build() {
  cd bpftrace-$pkgver

  mkdir -p build
  cd build
  cmake -DCMAKE_BUILD_TYPE=Release -DCMAKE_INSTALL_PREFIX=/usr ..
  make
}

package() {
  cd bpftrace-$pkgver/build

  make DESTDIR="$pkgdir" install
}
